buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def projectName = 'jdkgdxds_interop'
version "$VERSION_NAME"
group 'com.github.tommyettinger'

//api "com.github.tommyettinger:jdkgdxds_interop:1.3.0.0"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
            options.tags('apiNote:a:"API Note"', 'implSpec:a:"Implementation Requirements"', 'implNote:a:"Implementation Note"')
        }
    }
}
javadoc.options.source("17")
javadoc.destinationDir = file("docs/apidocs")

sourceSets.test.resources.srcDirs = [ file('src/test/resources').path ]

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(JavaCompile) {
            options.release.set(8)
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    api "com.github.tommyettinger:digital:$digitalVersion"
    api "com.github.tommyettinger:juniper:$juniperVersion"
    api "com.github.tommyettinger:jdkgdxds:$jdkgdxdsVersion"
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "junit:junit:4.13.2"
}
